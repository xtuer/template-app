plugins {
    id 'war'
    id 'java'
    id "org.gretty" version "2.3.1"
    id 'org.hidetake.ssh' version '2.9.0'
}

gretty {
    httpPort = 8080
    contextPath = ''
    servletContainer = 'tomcat8'

    inplaceMode  = 'hard'
    debugSuspend = false
    managedClassReload      = true
    recompileOnSourceChange = true

    // 升级 gretty 自带的 springloaded
    jvmArgs = ["-javaagent:${project.projectDir}/springloaded-1.2.8.RELEASE.jar", '-noverify']
}

////////////////////////////////////////////////////////////////////////////////
//                                   Maven 依赖                               //
////////////////////////////////////////////////////////////////////////////////
repositories {
    mavenCentral()
}

ext {
    // 运行和打包的环境选择, 默认是开发环境
    // 获取 gradle 参数中 env 的值: gradle -Denv=production clean build
    // 构建 gradle clean build
    //     gradle -Denv=production clean build
    // 部署 gradle clean deploy
    //     gradle -Denv=production clean deploy
    environment = System.getProperty("env", "development")
    war.archiveName = 'ROOT.zip' // 打包的文件名
}

ext.versions = [
    spring   : '5.0.2.RELEASE',
    servlet  : '4.0.0',
    fastjson : '1.2.41',
    thymeleaf: '3.0.9.RELEASE',
    lombok   : '1.16.18',
    junit    : '4.12'
]

dependencies {
    compile(
            "org.springframework:spring-webmvc:${versions.spring}", // Spring MVC
            "org.springframework:spring-context-support:${versions.spring}",
            "com.alibaba:fastjson:${versions.fastjson}",  // JSON
            "org.thymeleaf:thymeleaf:${versions.thymeleaf}",
            "org.thymeleaf:thymeleaf-spring5:${versions.thymeleaf}"
    )

    compileOnly("org.projectlombok:lombok:${versions.lombok}")
    compileOnly("javax.servlet:javax.servlet-api:${versions.servlet}")
    testCompile("org.springframework:spring-test:${versions.spring}")
    testCompile("junit:junit:${versions.junit}")
}

////////////////////////////////////////////////////////////////////////////////
//                                  资源动态替换                                //
////////////////////////////////////////////////////////////////////////////////
processResources {
    // src/main/resources 下的文件中 @key@ 的内容使用 config.groovy 里对应的进行替换
    println "==> Load configuration for '${environment}'"
    def configFile = file('config.groovy') // 配置文件
    def props = new ConfigSlurper(environment).parse(configFile.toURI().toURL()).toProperties()

    from(sourceSets.main.resources.srcDirs) {
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: props)
    }
}

////////////////////////////////////////////////////////////////////////////////
//                                    Deploy                                  //
////////////////////////////////////////////////////////////////////////////////
remotes {
    server {
        host = '192.168.82.133'
        user = 'root'
        // password = 'xxx'
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
    }
}

ssh.settings {
    knownHosts = allowAnyHosts
}

task deploy(dependsOn: war) {
    def targetDir = '/data/xtuer.com'
    doLast {
        ssh.run {
            session(remotes.server) {
                put from: "${buildDir}/libs/${war.archiveName}", into: "${targetDir}"
                execute """
                    source /root/.bash_profile;
                    /usr/local/tomcat/bin/shutdown.sh;
                    rm -rf ${targetDir}/ROOT;
                    unzip -u ${targetDir}/${war.archiveName} -d ${targetDir}/ROOT > /dev/null;
                    /usr/local/tomcat/bin/startup.sh;
                    rm -rf ${targetDir}/${war.archiveName};
                """
            }
        }
    }
}

////////////////////////////////////////////////////////////////////////////////
//                                    JVM                                     //
////////////////////////////////////////////////////////////////////////////////
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}
